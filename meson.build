project('bible', version: '0.1', meson_version: '>=0.40.0')

python = import('python3')
project_id = 'com.github.dahenson.bible'

message('Looking for dependencies')
python_bin = python.find_python()
if not python_bin.found()
	error('No valid python3 binary found')
else
	message('Found python3 binary')
endif
dependency('glib-2.0')
dependency('gtk+-3.0', version:'>=3.14')

python_dir = join_paths(get_option('prefix'), python.sysconfig_path('purelib'))
LIBEXEC_DIR = join_paths(get_option('prefix'), get_option('libexecdir'))
DATA_DIR = join_paths(get_option('prefix'), get_option('datadir'), meson.project_name())
bindir = join_paths(get_option('prefix'), get_option('bindir'))

conf = configuration_data()
conf.set('PACKAGE_URL', 'https://github.com/dahenson/bible')
conf.set('DATA_DIR', DATA_DIR)
conf.set('LOCALE_DIR', join_paths(get_option('prefix'), get_option('datadir'), 'locale'))
conf.set('PYTHON_DIR', python_dir)
conf.set('PYTHON_EXEC_DIR', join_paths(get_option('prefix'), python.sysconfig_path('stdlib')))
conf.set('libexecdir', LIBEXEC_DIR)
conf.set('VERSION', meson.project_version())
conf.set('PYTHON', python_bin.path())

subdir('data')

install_subdir(
	'bible',
	install_dir: python_dir
)

message('Preparing init file')
configure_file(
	input: 'com.github.dahenson.bible.in',
	output: 'com.github.dahenson.bible',
	configuration: conf,
	install_dir: bindir
)

meson.add_install_script('meson_post_install.py')